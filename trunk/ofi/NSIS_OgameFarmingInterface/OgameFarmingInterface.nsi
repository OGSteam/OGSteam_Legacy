; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Programme d'aide au pillage sur Ogame"
!define PRODUCT_VERSION ""
!define PRODUCT_PUBLISHER "Mackila"
!define PRODUCT_WEB_SITE "http://mackila.com/phpBB2"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\OgameFarmingInterface.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

!define PROGRAM_PARAM_ROOT_KEY "HKCU"
!define PROGRAM_PARAM_KEY "SOFTWARE\Mackila\OgameFarmingInterface"

;SetCompressor /FINAL lzma
SetCompress off 
XPStyle on
BrandingText "WIL Mackila | Ogame.fr U21 | W!L POWER FOREVER | http://mackila.com/phpBB2/ "



VIAddVersionKey "ProductName" "Programme d'aide au pillage sur Ogame"
VIAddVersionKey "Comments" "Permet de piller beaucoup plus facilement les stocks de pigeons de l'univers"
VIAddVersionKey "CompanyName" "Mackila"
VIAddVersionKey "LegalTrademarks" ""
VIAddVersionKey "LegalCopyright" "Mackila"
VIAddVersionKey "FileDescription" "Installe l'aide au pillage pour Ogame"
VIAddVersionKey "FileVersion" "1.0.0"

VIProductVersion "1.0.0.0"


; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
;!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
;!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "French"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "${MODE}\Install OgameFarmingInterface.exe"
InstallDir "$PROGRAMFILES\Mackila\OgameFarmingInterface"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Function .onInit
FunctionEnd


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;rafaichit les icones
;
		!define SHCNE_ASSOCCHANGED 0x08000000
		!define SHCNF_IDLIST 0
	Function RefreshShellIcons
 ; 		System::Call 'shell32.dll::SHChangeNotify(i, i, i, i) v \
 ; 		(${SHCNE_ASSOCCHANGED}, ${SHCNF_IDLIST}, 0, 0)'
	FunctionEnd
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

 ; IsDotNETInstalled
 ;
 ; Usage:
 ;   Call IsDotNETInstalled
 ;   Pop $0
 ;   StrCmp $0 1 found.NETFramework no.NETFramework

 Function IsDotNETInstalled
   Push $0
   Push $1
   Push $2
   Push $3
   Push $4

   ReadRegStr $4 HKEY_LOCAL_MACHINE \
     "Software\Microsoft\.NETFramework" "InstallRoot"
   # remove trailing back slash
   Push $4
   Exch $EXEDIR
   Exch $EXEDIR
   Pop $4
   # if the root directory doesn't exist .NET is not installed
   IfFileExists $4 0 noDotNET

   StrCpy $0 0

   EnumStart:

     EnumRegKey $2 HKEY_LOCAL_MACHINE \
       "Software\Microsoft\.NETFramework\Policy"  $0
     IntOp $0 $0 + 1
     StrCmp $2 "" noDotNET

     StrCpy $1 0

     EnumPolicy:

       EnumRegValue $3 HKEY_LOCAL_MACHINE \
         "Software\Microsoft\.NETFramework\Policy\$2" $1
       IntOp $1 $1 + 1
        StrCmp $3 "" EnumStart
         IfFileExists "$4\$2.$3" foundDotNET EnumPolicy

   noDotNET:
     StrCpy $0 0
     Goto done

   foundDotNET:
     StrCpy $0 1

   done:
     Pop $4
     Pop $3
     Pop $2
     Pop $1
     Exch $0
 FunctionEnd


Section "SectionPrincipale" SEC01

  SetShellVarContext all

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 
; Vérification de l'installation du framework .net 2.0
; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  Call IsDotNETInstalled
  Pop $0
  StrCmp $0 1 DotNET_2_OK DotNET_2_Absent
DotNET_2_Absent:
  MessageBox MB_OK|MB_ICONEXCLAMATION "Installez le .net version 2.0."
  Abort "Le framework .net 2.0 n'est pas installé."
DotNET_2_OK:
 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 
; Copie du programme
; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  SetOutPath "$INSTDIR"
  SetOverwrite on
  File "..\OgameFarmingInterface\bin\Release\OgameFarmingInterface.exe"
  File "..\Updater\bin\Release\Updater.exe"
  SetOverwrite try
  File "..\OgameFarmingInterface\Resources\FichierOGR.ico"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Création des raccourcis du menu démarrer
; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  ;              "Nom du raccourcis"                       "Cible"                               "Paramètres"    "Fichier icone"                         "Index" "Mode de lancement" "" "Commentaire"
  CreateDirectory "$SMPROGRAMS\Ogame"
  CreateShortCut "$SMPROGRAMS\Ogame\Farming Interface.lnk" "$INSTDIR\OgameFarmingInterface.exe"  ""              "$INSTDIR\OgameFarmingInterface.exe"    "0"     "SW_SHOWNORMAL"     "" "Vous aide à piller plus efficacement le stock de pigeons présents dans votre univers."
  CreateDirectory "$SMPROGRAMS\Ogame\Maintenance"
  CreateShortCut "$SMPROGRAMS\Ogame\Désinstaller.lnk"      "$INSTDIR\uninst.exe"                 ""              "$INSTDIR\uninst.exe"                   "0"     "SW_SHOWNORMAL"     "" "Efface le programme de cet ordinateur."

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Association des fichiers .ogr avec l'outil
; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  WriteRegStr HKCR ".OGR" "" "FichiersDonneesOgame"
  WriteRegStr HKCR "FichiersDonneesOgame\DefaultIcon" "" "$INSTDIR\FichierOGR.ico"
  WriteRegStr HKCR "FichiersDonneesOgame\shell\open\command" "" '"$INSTDIR\OgameFarmingInterface.exe" "%1"'
  Call RefreshShellIcons

;FinSectionPrincipale:
SectionEnd

Section -AdditionalIcons
  CreateShortCut "$SMPROGRAMS\SISP\Maintenance\Désinstaller.lnk" "$INSTDIR\uninst.exe"            "" "" "" "" "" "Supprime le programme et tous ses composants de l'ordinateur."
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\OgameFarmingInterface.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\OgameFarmingInterface.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
;Ajout dans le registre de l'emplacement d'installation
  WriteRegStr HKLM "SOFTWARE\Mackila\OgameFarmingInterface" "CheminInstallation" "$INSTDIR"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) a été désinstallé avec succès de votre ordinateur."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Êtes-vous certains de vouloir désinstaller totalement $(^Name) et tous ses composants ?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  ; Raccourcis du menu démarrer
  SetShellVarContext all
  Delete "$SMPROGRAMS\Ogame\Farming Interface.lnk"
  Delete "$SMPROGRAMS\Ogame\Maintenance\Désinstaller.lnk"
  RMDir "$SMPROGRAMS\Ogame\Maintenance"
  RMDir "$SMPROGRAMS\Ogame"
  ; Fichiers
  Delete "$INSTDIR\Updater.exe"
  Delete "$INSTDIR\OgameFarmingInterface.exe"
  Delete "$INSTDIR\FichierOGR.ico"
  Delete "$INSTDIR\uninst.exe"
  ; Répertoires
  RMDir "$INSTDIR"
  ; Paramètre d'installation
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  ; Association .ogr
  DeleteRegKey HKCR ".ogr"
  DeleteRegKey HKCR "FichiersDonneesOgame"
  ; Fermeture...
  SetAutoClose true
SectionEnd
